# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/graphic/graphic_2d/graphic_config.gni")

## Build libnative_image.so {{{
config("native_image_config") {
  visibility = [ ":*" ]

  cflags = [
    "-Wall",
    "-Werror",
    "-g3",
  ]
}

config("libnative_image_public_config") {
  include_dirs = [
    "$graphic_2d_root/utils/sandbox",
    "$graphic_2d_root/interfaces/inner_api/surface",
    "include",
  ]
}

ohos_shared_library("libnative_image") {
  sanitize = {
    boundary_sanitize = true
    integer_overflow = true
    ubsan = true
  }
  sources = []
  if (surface_enable_gpu) {
    sources += [
      "src/native_image.cpp",
      "src/surface_image.cpp",
    ]
  }

  configs = [ ":native_image_config" ]

  public_configs = [ ":libnative_image_public_config" ]

  deps = [
    "$graphic_2d_root/frameworks/opengl_wrapper:EGL",
    "$graphic_2d_root/frameworks/opengl_wrapper:GLESv3",
  ]

  output_extension = "so"

  external_deps = [
    "c_utils:utils",
    "graphic_surface:surface",
    "graphic_surface:sync_fence",
    "hilog:libhilog",
    "ipc:ipc_single",
  ]

  part_name = "graphic_2d"
  subsystem_name = "graphic"
}
## Build libnative_image.so }}}
