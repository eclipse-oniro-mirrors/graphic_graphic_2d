# Copyright (c) 2024-2025 Huawei Device Co., Ltd.. All rights reserved.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/graphic/graphic_2d/ace_platforms.gni")
import("//foundation/graphic/graphic_2d/frameworks/text/config.gni")
import("//foundation/graphic/graphic_2d/graphic_config.gni")

ohos_source_set("text_ndk") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    cfi_vcall_icall_only = true
    debug = false
  }

  include_dirs = [
    "$graphic_2d_root/rosen/modules/render_service_base/include",
    "$graphic_2d_root/rosen/modules/render_service_base/src",
    "$graphic_2d_root/frameworks/text/service/global_config",
    "$rosen_root/modules/2d_graphics/drawing_ndk/include",
    "$rosen_text_root",
    "$rosen_text_root/adapter",
    "$rosen_text_root/interface/export",
    "$rosen_text_root/interface/export/ndk_include",
    "$rosen_text_root/service",
    "$rosen_text_root/service/texgine/src",
  ]
  deps = [
    "$rosen_root/modules/2d_graphics:2d_graphics",
    "$rosen_text_root:rosen_text",
  ]
  sources = [
    "drawing_font_manager/drawing_font_collection.cpp",
    "drawing_font_manager/drawing_font_mgr.cpp",
    "drawing_font_manager/drawing_register_font.cpp",
    "drawing_font_manager/drawing_text_font_descriptor.cpp",
    "drawing_font_manager/font_utils.cpp",
    "drawing_typography/drawing_text_global.cpp",
    "drawing_typography/drawing_text_line.cpp",
    "drawing_typography/drawing_text_lineTypography.cpp",
    "drawing_typography/drawing_text_run.cpp",
    "drawing_typography/drawing_text_typography.cpp",
  ]

  defines = []
  if (use_skia_txt) {
    defines += [ "USE_SKIA_TXT" ]
    include_dirs += [ "$rosen_text_root/adapter/skia" ]
  }
  if (is_arkui_x) {
    defines += [ "CROSS_PLATFORM" ]
  }
  if (rs_enable_gpu) {
    defines += [ "RS_ENABLE_GPU" ]
  }

  if (platform_is_ohos) {
    external_deps = [
      "bounds_checking_function:libsec_static",
      "hilog:libhilog",
      "icu:shared_icuuc",
    ]
  } else {
    cflags = [ "-std=c++17" ]
    deps += [ "$rosen_root/modules/platform:hilog" ]
    defines += [ "MODULE_DRAWING" ]
    external_deps = [
      "bounds_checking_function:libsec_static",
      "skia:skia_canvaskit",
    ]
  }
  part_name = "graphic_2d"
  subsystem_name = "graphic"
}
