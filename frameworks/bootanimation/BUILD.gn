# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")

## Build bootanimation {{{
config("bootanimation_config") {
  visibility = [ ":*" ]

  include_dirs = [
    "include",
    "//foundation/graphic/standard/utils/log",
  ]

  cflags = [
    "-Wall",
    "-Werror",
    "-g3",
  ]

  if ("${product_name}" == "rk3566" || "${product_name}" == "rk3568" ||
      "${product_name}" == "watchos" || "${product_name}" == "Hi3516DV300" ||
      "${product_name}" == "ohos-arm64") {
    defines = [ "ACE_DISABLE_GL" ]
  } else {
    defines = [ "ACE_ENABLE_GL" ]
  }
}

ohos_executable("bootanimation") {
  install_enable = true

  sources = [
    "src/boot_animation.cpp",
    "src/main.cpp",
    "src/util.cpp",
  ]

  configs = [
    ":bootanimation_config",
    "//utils/native/base:utils_config",
  ]

  deps = [
    "//base/global/resmgr_standard/frameworks/resmgr:global_resmgr",
    "//base/hiviewdfx/hilog/interfaces/native/innerkits:libhilog",
    "//foundation/ace/ace_engine/build/external_config/flutter/skia:ace_skia_ohos",
    "//foundation/distributedschedule/dmsfwk/services/dtbschedmgr:distributedschedsvr",
    "//foundation/distributedschedule/samgr/interfaces/innerkits/samgr_proxy:samgr_proxy",
    "//foundation/graphic/standard:libsurface",
    "//foundation/graphic/standard:libvsync_client",
    "//foundation/graphic/standard:libwmclient",
    "//foundation/graphic/standard:libwmservice",
    "//foundation/graphic/standard/rosen/modules/2d_graphics:2d_graphics",
    "//foundation/graphic/standard/rosen/modules/render_service_base/src/platform:platform",
    "//foundation/graphic/standard/rosen/modules/render_service_client:librender_service_client",
    "//foundation/windowmanager/dm:libdm",
    "//foundation/windowmanager/dmserver:libdms",
    "//foundation/windowmanager/wm:libwm",
    "//foundation/windowmanager/wmserver:libwms",
    "//third_party/zlib:libz",
  ]

  external_deps = [
    "bytrace_standard:bytrace_core",
    "ipc:ipc_core",
  ]

  if (is_standard_system) {
    external_deps += [ "startup_l2:syspara" ]
  } else {
    external_deps += [ "startup:syspara" ]
  }

  part_name = "graphic_standard"
  subsystem_name = "graphic"
}

## Build bootanimation }}}

## Install data/*.jpg to /system/etc/init/ {{{
ohos_prebuilt_etc("bootanimation_pics") {
  source = "data/bootpic.zip"
  relative_install_dir = "init"
  part_name = "graphic_standard"
  subsystem_name = "graphic"
}
## Install data/*.jpg to /system/etc/init/ }}}
