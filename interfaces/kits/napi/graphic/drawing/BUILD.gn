# Copyright (c) 2024 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/graphic/graphic_2d/frameworks/text/config.gni")
import("//foundation/graphic/graphic_2d/graphic_config.gni")

group("drawingnapi") {
  deps = [
    ":drawing_napi",
    ":drawing_napi_impl",
  ]
}

config("drawing_config") {
  include_dirs = [ "../drawing" ]
}

config("local_drawing_config") {
  visibility = [ ":*" ]

  include_dirs = [
    "../drawing",
    "$graphic_2d_root/frameworks/text/adapter/skia",
    "$graphic_2d_root/rosen/modules/2d_graphics/include",
    "$graphic_2d_root/rosen/modules/2d_graphics/src",
    "$graphic_2d_root/rosen/modules/2d_graphics/src/drawing/engine_adapter",
    "$graphic_2d_root/rosen/modules/render_service_base/include",
  ]
}

ohos_shared_library("drawing_napi") {
  sources = [ "drawing_module.cpp" ]

  configs = [
    ":drawing_config",
    ":local_drawing_config",
  ]

  deps = [ ":drawing_napi_impl" ]

  external_deps = [
    "napi:ace_napi",
    "skia:skia_canvaskit",
  ]

  relative_install_dir = "module/graphics"
  part_name = "graphic_2d"
  subsystem_name = "graphic"
}

if (is_arkui_x) {
  ohos_source_set("drawing_napi_impl") {
    sources = [
      "brush_napi/js_brush.cpp",
      "canvas_napi/js_canvas.cpp",
      "color_filter_napi/js_color_filter.cpp",
      "enum_napi/js_enum.cpp",
      "font_napi/js_font.cpp",
      "font_napi/js_typeface.cpp",
      "font_napi/js_typefacearguments.cpp",
      "image_filter_napi/js_image_filter.cpp",
      "js_drawing_init.cpp",
      "js_drawing_utils.cpp",
      "lattice_napi/js_lattice.cpp",
      "mask_filter_napi/js_mask_filter.cpp",
      "matrix_napi/js_matrix.cpp",
      "path_effect_napi/js_path_effect.cpp",
      "path_iterator_napi/js_path_iterator.cpp",
      "path_napi/js_path.cpp",
      "pen_napi/js_pen.cpp",
      "rect_utils_napi/js_rect_utils.cpp",
      "region_napi/js_region.cpp",
      "roundRect_napi/js_roundrect.cpp",
      "sampling_options_napi/js_sampling_options.cpp",
      "shader_effect_napi/js_shader_effect.cpp",
      "shadow_layer_napi/js_shadow_layer.cpp",
      "text_blob_napi/js_text_blob.cpp",
      "tool_napi/js_tool.cpp",
      "drawing_module.cpp",
    ]

    defines = [ "ROSEN_ARKUI_X" ]
    configs = [ ":local_drawing_config" ]
    public_configs = [ ":drawing_config" ]

    include_dirs = [
      "//foundation/multimedia/image_framework/interfaces/kits/js/common/include",
      "//foundation/multimedia/image_framework/interfaces/innerkits/include",
      "//foundation/graphic/graphic_surface/interfaces/inner_api/surface"
    ]

    deps = [
      "$graphic_2d_root/rosen/modules/2d_graphics:2d_graphics",
      "$graphic_2d_root/rosen/modules/render_service_base:librender_service_base_static",
      "//foundation/arkui/napi:ace_napi",
    ]
    public_deps = [
      "//foundation/multimedia/image_framework/interfaces/innerkits:image_native",
    ]
    defines += [ "MODULE_DRAWING" ]
    cflags_cc = [ "-std=c++17" ]

    part_name = "graphic_2d"
    subsystem_name = "graphic"
  }
} else {
  ohos_shared_library("drawing_napi_impl") {
    sources = [
      "brush_napi/js_brush.cpp",
      "canvas_napi/js_canvas.cpp",
      "color_filter_napi/js_color_filter.cpp",
      "enum_napi/js_enum.cpp",
      "font_napi/js_font.cpp",
      "font_napi/js_typeface.cpp",
      "font_napi/js_typefacearguments.cpp",
      "image_filter_napi/js_image_filter.cpp",
      "js_drawing_init.cpp",
      "js_drawing_utils.cpp",
      "lattice_napi/js_lattice.cpp",
      "mask_filter_napi/js_mask_filter.cpp",
      "matrix_napi/js_matrix.cpp",
      "path_effect_napi/js_path_effect.cpp",
      "path_iterator_napi/js_path_iterator.cpp",
      "path_napi/js_path.cpp",
      "pen_napi/js_pen.cpp",
      "rect_utils_napi/js_rect_utils.cpp",
      "region_napi/js_region.cpp",
      "roundRect_napi/js_roundrect.cpp",
      "sampling_options_napi/js_sampling_options.cpp",
      "shader_effect_napi/js_shader_effect.cpp",
      "shadow_layer_napi/js_shadow_layer.cpp",
      "text_blob_napi/js_text_blob.cpp",
      "tool_napi/js_tool.cpp",
    ]
    defines = []
    configs = [ ":local_drawing_config" ]
    public_configs = [ ":drawing_config" ]

    deps = [
      "$graphic_2d_root/rosen/modules/2d_graphics:2d_graphics",
      "$graphic_2d_root/rosen/modules/render_service_base:librender_service_base",
      "$rosen_text_root:rosen_text",
    ]

    external_deps = [ "napi:ace_napi" ]

    if (current_os == "ohos" || current_os == "ohos_ng") {
      sanitize = {
        boundary_sanitize = true
        integer_overflow = true
        ubsan = true
      }
      external_deps += [
        "ability_runtime:abilitykit_native",
        "ability_runtime:app_context",
        "ability_runtime:napi_common",
        "eventhandler:libeventhandler",
        "hilog:libhilog",
        "image_framework:image_native",
        "init:libbegetutil",
        "resource_management:global_resmgr",
      ]
      deps += [ "$graphic_2d_root/utils:libgraphic_utils" ]
      deps += [ "$graphic_2d_root/interfaces/kits/napi/graphic/color_manager:color_space_object_convertor" ]
      defines += [ "ROSEN_OHOS" ]
      cflags_cc = [ "-std=c++17" ]
    } else {
      defines += [ "MODULE_DRAWING" ]
      cflags_cc = [ "-std=c++17" ]
    }

    if (current_os == "mingw") {
      defines += [ "WINDOWS_PLATFORM" ]
    }

    innerapi_tags = [ "platformsdk" ]
    part_name = "graphic_2d"
    subsystem_name = "graphic"
  }
}
