# Copyright (C) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")
import("//foundation/graphic/graphic_2d/graphic_config.gni")

group("ani_drawing") {
  deps = [
    ":drawing_ani",
    ":drawing_ets",
  ]
}

config("drawing_ani_config") {
  include_dirs = [
    "$graphic_2d_root/interfaces/kits/ani/drawing",
  ]
  defines = [
    "ROSEN_OHOS"
  ]
}

config("graphic_ani_drawing_config") {
  visibility = [ ":*" ]
  defines = []
  include_dirs = [
    ".",
    "$graphic_2d_root/rosen/modules/2d_graphics/include",
    "$graphic_2d_root/rosen/modules/2d_graphics/src",
    "$graphic_2d_root/rosen/modules/2d_graphics/src/drawing/engine_adapter",
  ]

  cflags = [
    "-Wall",
    "-Werror",
    "-g3",
    "-Wno-pointer-arith",
    "-Wno-non-virtual-dtor",
    "-Wno-missing-field-initializers",
    "-Wno-c++11-narrowing",
    "-Wno-unused-but-set-variable",
  ]

  if (current_os == "ohos" || current_os == "ohos_ng") {
    defines += [ "ROSEN_OHOS" ]
  }
}

ohos_shared_library("drawing_ani") {
  branch_protector_ret = "pac_ret"

  sanitize = {
    cfi = true
    cfi_cross_dso = true
    cfi_vcall_icall_only = true
    debug = false
  }

  public_configs = [
    ":drawing_ani_config",
    "$graphic_2d_root/rosen/modules/2d_graphics:export_config",
  ]

  configs = [
    ":graphic_ani_drawing_config",
  ]

  sources = [
    "ani_drawing_module.cpp",
    "ani_drawing_utils.cpp",
    "lattice_ani/ani_lattice.cpp",
    "matrix_ani/ani_matrix.cpp",
    "path_iterator_ani/ani_path_iterator.cpp",
    "path_ani/ani_path.cpp",
    "region_ani/ani_region.cpp",
    "round_rect_ani/ani_round_rect.cpp",
    "brush_ani/ani_brush.cpp",
    "canvas_ani/ani_canvas.cpp",
    "color_filter_ani/ani_color_filter.cpp",
    "font_ani/ani_font.cpp",
    "pen_ani/ani_pen.cpp",
    "sampling_options_ani/ani_sampling_options.cpp",
    "typeface_ani/ani_typeface.cpp",
    "typeface_arguments_ani/ani_typeface_arguments.cpp",
  ]

  deps = [
    "$graphic_2d_root/rosen/modules/2d_graphics:2d_graphics",
  ]

  external_deps = [
    "c_utils:utils",
    "hilog:libhilog",
    "image_framework:image_ani",
    "runtime_core:ani",
    "runtime_core:libarkruntime",
  ]

  public_external_deps = [
    "image_framework:image",
  ]

  part_name = "graphic_2d"
  subsystem_name = "graphic"
  output_extension = "so"
}

generate_static_abc("drawing") {
  base_url = "./ets"
  files = [
    "ets/@ohos.graphics.common2D.ets",
    "ets/@ohos.graphics.drawing.ets",
  ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/drawing.abc"
}

generate_static_abc("drawing_test") {
  base_url = "./ets"
  files = [
    "ets/drawing_test.ets"
  ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/drawing_test.abc"
}

ohos_prebuilt_etc("drawing_ets") {
  source = "$target_out_dir/drawing.abc"
  module_install_dir = "framework"
  subsystem_name = "graphic"
  part_name = "graphic_2d"
  deps = [
    ":drawing",
  ]
}