# Copyright (c) 2022-2024 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/test.gni")
import("//foundation/graphic/graphic_2d/ace_platforms.gni")
import("//foundation/graphic/graphic_2d/graphic_config.gni")

module_output_path = "graphic/rosen_engine/render_service_base/platform/ohos"

##############################  RSRenderServiceBasePlatformTest  ##################################
ohos_unittest("RSRenderServiceBasePlatformTest") {
  module_out_path = module_output_path

  sources = [
    "rs_accessibility_test.cpp",
    "rs_ashmem_helper_test.cpp",
    "rs_event_detector_test.cpp",
    "rs_event_manager_test.cpp",
    "rs_innovation_test.cpp",
    "rs_jank_stats_test.cpp",
    "rs_log_test.cpp",
    "rs_marshalling_helper_test.cpp",
    "rs_node_stats_test.cpp",
    "rs_render_service_client_test.cpp",
    "rs_render_service_connect_hub_test.cpp",
    "rs_render_service_connection_proxy_test.cpp",
    "rs_render_service_proxy_test.cpp",
    "rs_surface_ohos_converter_test.cpp",
    "rs_surface_ohos_test.cpp",
    "rs_system_properties_test.cpp",
    "rs_vsync_client_ohos_test.cpp",
  ]

  cflags = [
    "-Dprivate=public",
    "-Dprotected=public",
  ]

  configs = [
    ":render_test",
    "$graphic_2d_root/rosen/modules/render_service_base:export_config",
  ]

  include_dirs = [
    "$graphic_2d_root/rosen/modules/render_service_base/include",
    "$graphic_2d_root/rosen/modules/render_service_base/src",
    "$graphic_2d_root/rosen/modules/render_service_client/core",
    "$graphic_2d_root/rosen/include",
    "$graphic_2d_root/rosen/test/include",
    "//utils/system/safwk/native/include",
    "$graphic_2d_root/prebuilts/librarys/gpu/include",
    "//drivers/peripheral/display/interfaces/include/",
    "$graphic_2d_root/rosen/modules/render_service_client/core",
    "$graphic_2d_root/utils/log",
  ]

  deps = [
    "$graphic_2d_root/rosen/modules/render_service_base:librender_service_base",
    "$graphic_2d_root/rosen/modules/render_service_client:librender_service_client",
    "//third_party/googletest:gtest_main",
  ]

  public_deps = [
    "$graphic_2d_root/rosen/modules/composer/vsync:libvsync",
    "//foundation/multimedia/image_framework/interfaces/innerkits:image_native",
  ]
  public_external_deps = [
    "graphic_surface:sync_fence",
    "skia:skia_canvaskit",
  ]

  if (defined(input_ext_feature_magiccursor) && input_ext_feature_magiccursor) {
    defines = [ "OHOS_BUILD_ENABLE_MAGICCURSOR" ]
  }

  if (ace_enable_gpu) {
    public_deps += [
      "$graphic_2d_root/frameworks/opengl_wrapper:EGL",
      "$graphic_2d_root/frameworks/opengl_wrapper:GLESv3",
    ]

    public_external_deps += [ "openssl:libcrypto_shared" ]
  }

  external_deps = [
    "c_utils:utils",
    "eventhandler:libeventhandler",
    "hilog:libhilog",
    "hisysevent:libhisysevent",
    "hitrace:hitrace_meter",
    "init:libbegetutil",
    "ipc:ipc_core",
    "samgr:samgr_proxy",
    "skia:skia_canvaskit",
  ]

  subsystem_name = "graphic"
}

###############################################################################
config("render_test") {
  visibility = [ ":*" ]
  include_dirs = [
    "$ace_root",
    "$graphic_2d_root/rosen/modules/render_service_base",
  ]
}

group("unittest") {
  testonly = true

  deps = [ ":RSRenderServiceBasePlatformTest" ]
}
