# Copyright (c) 2022-2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/test.gni")
import("//foundation/graphic/graphic_2d/ace_platforms.gni")
import("//foundation/graphic/graphic_2d/graphic_config.gni")

module_output_path = "graphic_2d/graphic_2d/render_service_base/common"

##############################  RSRenderServiceBaseCommonTest  ##################################
ohos_unittest("RSRenderServiceBaseCommonTest") {
  module_out_path = module_output_path

  sources = [
    "rs_background_thread_test.cpp",
    "rs_color_test.cpp",
    "rs_common_def_test.cpp",
    "rs_common_hook_test.cpp",
    "rs_common_tools_test.cpp",
    "rs_obj_abs_geometry_test.cpp",
    "rs_occlusion_region_test.cpp",
    "rs_rect_test.cpp",
    "rs_rectangles_merger_test.cpp",
    "rs_special_layer_manager_test.cpp",
    "rs_switching_thread_test.cpp",
    "rs_thread_handler_generic_test.cpp",
    "rs_thread_looper_generic_test.cpp",
    "rs_thread_looper_impl_test.cpp",
    "rs_threshold_detector_test.cpp",
    "rs_vector3_test.cpp",
    "rs_vector4_test.cpp",
    "rs_xcollie_test.cpp",
  ]

  cflags = [
    "-Dprivate=public",
    "-Dprotected=public",
  ]

  configs = [
    ":common_test",
    "$graphic_2d_root/rosen/modules/render_service_base:export_config",
  ]

  include_dirs = [
    "$graphic_2d_root/rosen/modules/2d_graphics/include",
    "$graphic_2d_root/rosen/modules/2d_graphics/src",
    "$graphic_2d_root/rosen/modules/render_service_base/include",
    "$graphic_2d_root/rosen/modules/render_service_base/src",
    "$graphic_2d_root/rosen/modules/render_service_base/src/common/",
    "$graphic_2d_root/rosen/include",
    "$graphic_2d_root/rosen/test/include",
  ]
  if (defined(use_rosen_drawing) && use_rosen_drawing) {
    public_deps = [ "$graphic_2d_root/rosen/modules/2d_graphics:2d_graphics" ]
  }
  if (defined(graphic_2d_math_tools_configs.vendor_root)) {
    sources +=
        graphic_2d_math_tools_configs.librender_service_ext_math_tools_test
    sources -=
        graphic_2d_math_tools_configs.librender_service_ext_math_tools_test_del
  }
  deps = [
    "$graphic_2d_root/rosen/modules/render_service_base:librender_service_base",
    "$graphic_2d_root/rosen/modules/render_service_base:render_service_base_src",
  ]
  external_deps = [
    "c_utils:utils",
    "eventhandler:libeventhandler",
    "googletest:gtest_main",
    "hilog:libhilog",
    "image_framework:image_native",
    "init:libbegetutil",
    "ipc:ipc_core",
    "skia:skia_canvaskit",
  ]

  subsystem_name = "graphic"
}

###############################################################################
config("common_test") {
  visibility = [ ":*" ]
  include_dirs = [
    "$ace_root",
    "$graphic_2d_root/rosen/modules/render_service_base",
  ]
}

group("unittest") {
  testonly = true
  deps = [ ":RSRenderServiceBaseCommonTest" ]
}
