# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/test.gni")
import("//foundation/graphic/graphic_2d/graphic_config.gni")
import("$graphic_2d_root/rosen/modules/render_service_base/config.gni")

module_output_path =
    "graphic_2d/graphic_2d/rosen_engine/render_service/transaction"

group("unittest") {
  testonly = true

  deps = [
    ":RSIRenderServiceConnectionIpcInterfaceCodeAccessVerifierTest",
    ":RSIRenderServiceIpcInterfaceCodeAccessVerifierTest",
    ":RSRenderServiceConnectionStubTest",
    ":RSRenderServiceStubTest",
    ":RSUnmarshalThreadTest",
  ]
}

###############################################################################
config("pipeline_test") {
  include_dirs = [ "$graphic_2d_root/rosen/modules/render_service/core" ]
}

## Build RSIRenderServiceConnectionIpcInterfaceCodeAccessVerifierTest
ohos_unittest("RSIRenderServiceConnectionIpcInterfaceCodeAccessVerifierTest") {
  module_out_path = module_output_path
  sources = [
    "rs_irender_service_connection_ipc_interface_code_access_verifier_test.cpp",
  ]
  deps = [ ":rs_test_common" ]
  external_deps = []
  if (defined(global_parts_info) && defined(global_parts_info.sensors_sensor)) {
    external_deps += [ "sensor:sensor_interface_native" ]
  }
  defines = []
  defines += gpu_defines
  if (defined(enable_ipc_security) && enable_ipc_security) {
    defines += [ "ENABLE_IPC_SECURITY" ]
    external_deps += [ "access_token:libaccesstoken_sdk" ]
  }
}

## Build RSIRenderServiceIpcInterfaceCodeAccessVerifierTest
ohos_unittest("RSIRenderServiceIpcInterfaceCodeAccessVerifierTest") {
  module_out_path = module_output_path
  sources = [ "rs_irender_service_ipc_interface_code_access_verifier_test.cpp" ]
  deps = [ ":rs_test_common" ]
}

## Build RSRenderServiceConnectionStubTest
ohos_unittest("RSRenderServiceConnectionStubTest") {
  module_out_path = module_output_path
  sources = [
    "../pipeline/mock/mock_hdi_device.cpp",
    "rs_render_service_connection_stub_test.cpp",
  ]
  deps = [ ":rs_test_common" ]
  external_deps = [ "googletest:gmock_main" ]
  if (defined(global_parts_info) && defined(global_parts_info.sensors_sensor)) {
    external_deps += [ "sensor:sensor_interface_native" ]
  }
  defines = []
  defines += gpu_defines
}

## Build RSRenderServiceStubTest
ohos_unittest("RSRenderServiceStubTest") {
  module_out_path = module_output_path
  sources = [ "rs_render_service_stub_test.cpp" ]
  deps = [ ":rs_test_common" ]
  external_deps = []
  if (defined(global_parts_info) && defined(global_parts_info.sensors_sensor)) {
    external_deps += [ "sensor:sensor_interface_native" ]
  }
  defines = []
  defines += gpu_defines
}

## Build rs_test_common.a {{{
config("rs_test_common_public_config") {
  include_dirs = [
    "$graphic_2d_root/rosen/include",
    "$graphic_2d_root/rosen/modules/hyper_graphic_manager/core/config",
    "$graphic_2d_root/rosen/modules/hyper_graphic_manager/core/frame_rate_manager",
    "$graphic_2d_root/rosen/modules/hyper_graphic_manager/core/hgm_screen_manager",
    "$graphic_2d_root/rosen/modules/hyper_graphic_manager/core/utils",
    "$graphic_2d_root/rosen/modules/render_service/core",
    "$graphic_2d_root/rosen/modules/render_service/core/feature_cfg",
    "$graphic_2d_root/rosen/modules/render_service/core/feature_cfg/feature_param",
    "$graphic_2d_root/rosen/modules/render_service/core/feature_cfg/feature_param/extend_feature",
    "$graphic_2d_root/rosen/modules/render_service/core/feature_cfg/feature_param/performance_feature",
    "$graphic_2d_root/rosen/modules/render_service/core/feature_cfg/xml_parser",
    "$graphic_2d_root/rosen/modules/render_service/core/feature_cfg/xml_parser/extend_feature_parser",
    "$graphic_2d_root/rosen/modules/render_service/core/feature_cfg/xml_parser/performance_feature_parser",
    "$graphic_2d_root/rosen/modules/render_service_client/core",
    "$graphic_2d_root/rosen/modules/render_service_base/include",
    "$graphic_2d_root/rosen/modules/render_service_base/src",
    "$graphic_2d_root/rosen/test/include",
    "$graphic_2d_root/rosen/modules/render_service_base/include/platform/ohos/",
    "$graphic_2d_root/rosen/test/render_service/render_service/unittest/pipeline/mock",
  ]

  cflags = [
    "-Wall",
    "-Werror",
    "-g3",
    "-Dprivate=public",
    "-Dprotected=public",
  ]
}

## Build RSUnmarshalThreadTest
ohos_unittest("RSUnmarshalThreadTest") {
  module_out_path = module_output_path
  sources = [ "rs_unmarshal_thread_test.cpp" ]
  deps = [ ":rs_test_common" ]
  external_deps = [
    "c_utils:utils",
    "ffrt:libffrt",
    "hilog:libhilog",
  ]
  defines = []
  defines += gpu_defines
  if (defined(use_rosen_drawing) && use_rosen_drawing) {
    defines += [ "USE_ROSEN_DRAWING" ]
  }
  if (defined(use_video_processing_engine) && use_video_processing_engine) {
    include_dirs = []
    defines += [ "USE_VIDEO_PROCESSING_ENGINE" ]
    external_deps += [
      "drivers_interface_display:libdisplay_commontype_proxy_1.0",
      "video_processing_engine:videoprocessingengine",
    ]
  }
}

ohos_static_library("rs_test_common") {
  visibility = [ ":*" ]
  testonly = true
  defines = []
  defines += gpu_defines

  public_configs = [ ":rs_test_common_public_config" ]

  public_deps = [
    "$graphic_2d_root/rosen/modules/composer:libcomposer",
    "$graphic_2d_root/rosen/modules/render_service:librender_service",
    "$graphic_2d_root/rosen/modules/render_service_base:librender_service_base",
    "$graphic_2d_root/rosen/modules/render_service_client:librender_service_client",
  ]

  public_external_deps = [
    "c_utils:utils",
    "hilog:libhilog",
    "hitrace:hitrace_meter",
    "init:libbegetutil",
    "ipc:ipc_core",
  ]

  if (use_memmgr_plugin || use_memmgr) {
    public_external_deps += [ "memmgr:memmgrclient" ]
  }

  subsystem_name = "graphic"
  part_name = "graphic_2d"
}
