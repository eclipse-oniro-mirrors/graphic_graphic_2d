# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/test.gni")
import("//foundation/graphic/graphic_2d/graphic_config.gni")

module_output_path = "graphic_2d/graphic_2d/rosen_engine/render_service/feature"

group("unittest") {
  testonly = true

  deps = [
    ":RSHeteroHDRHpaeTest",
  ]
}

###############################################################################
config("hetero_hdr_test") {
  include_dirs = [
    "$graphic_2d_root/rosen/modules/render_service/core",
    "$graphic_2d_root/rosen/modules/render_service_base/include",
    "$graphic_2d_root/rosen/modules/render_service_base/src",
    "$graphic_2d_ext_root",
  ]

  cflags = [
    "-Wall",
    "-Werror",
    "-g3",
    "-Dprivate=public",
    "-Dprotected=public",
  ]
}

ohos_unittest("RSHeteroHDRHpaeTest") {
  module_out_path = module_output_path
  sources = [
    "$graphic_2d_ext_root/hetero_hdr/test/unittest/rs_hetero_hdr_buffer_layer_test.cpp",
    "$graphic_2d_ext_root/hetero_hdr/test/unittest/rs_hetero_hdr_manager_test.cpp",
    "$graphic_2d_ext_root/hetero_hdr/test/unittest/rs_hetero_hdr_hpae_test.cpp",
    "$graphic_2d_ext_root/hetero_hdr/test/unittest/rs_hdr_pattern_manager_test.cpp",
  ]
  public_configs = [ ":hetero_hdr_test" ]
  deps = [
    ":rs_test_common",
    "$graphic_2d_root/rosen/modules/composer:libcomposer",
    "$graphic_2d_root/rosen/modules/render_service:librender_service",
    "$graphic_2d_root/rosen/modules/render_service_base:librender_service_base",
    "$graphic_2d_root/rosen/modules/render_service_client:librender_service_client",
    "$graphic_2d_root/rosen/modules/hyper_graphic_manager:libhyper_graphic_manager",
    "$graphic_2d_root/utils:socketpair",
  ]
  external_deps = [
    "c_utils:utils",
    "ffrt:libffrt",
    "googletest:gmock_main",
    "googletest:gtest_main",
    "hilog:libhilog",
    "hitrace:hitrace_meter",
    "init:libbegetutil",
    "ipc:ipc_core",
    "memmgr:memmgrclient",
    "drivers_interface_himediacomm:libhimediacomm_copybit_pt_proxy_1.0",
    "hdf_core:libhdi",
    "mhc_client:libmhc_client",
  ]
  if (defined(global_parts_info) && defined(global_parts_info.sensors_sensor)) {
    external_deps += [ "sensor:sensor_interface_native" ]
  }
  defines = []
  defines += gpu_defines
  if (defined(use_rosen_drawing) && use_rosen_drawing) {
    defines += [ "USE_ROSEN_DRAWING" ]
  }
  if (defined(use_video_processing_engine) && use_video_processing_engine) {
    defines += [ "USE_VIDEO_PROCESSING_ENGINE" ]
    external_deps += [
      "drivers_interface_display:libdisplay_commontype_proxy_1.0",
      "video_processing_engine:videoprocessingengine",
    ]
  }
}

## Build rs_test_common.a {{{
config("rs_test_common_public_config") {
  include_dirs = [
    "$graphic_2d_root/rosen/include",
    "$graphic_2d_root/rosen/modules/hyper_graphic_manager/core/config",
    "$graphic_2d_root/rosen/modules/hyper_graphic_manager/core/frame_rate_manager",
    "$graphic_2d_root/rosen/modules/hyper_graphic_manager/core/hgm_screen_manager",
    "$graphic_2d_root/rosen/modules/hyper_graphic_manager/core/soft_vsync_manager",
    "$graphic_2d_root/rosen/modules/hyper_graphic_manager/core/utils",
    "$graphic_2d_root/rosen/modules/render_service/main",
    "$graphic_2d_root/rosen/modules/render_service/core",
    "$graphic_2d_root/rosen/modules/render_service/core/feature_cfg",
    "$graphic_2d_root/rosen/modules/render_service/core/feature_cfg/feature_param",
    "$graphic_2d_root/rosen/modules/render_service/core/feature_cfg/feature_param/extend_feature",
    "$graphic_2d_root/rosen/modules/render_service/core/feature_cfg/feature_param/performance_feature",
    "$graphic_2d_root/rosen/modules/render_service/core/feature_cfg/xml_parser",
    "$graphic_2d_root/rosen/modules/render_service/core/feature_cfg/xml_parser/extend_feature_parser",
    "$graphic_2d_root/rosen/modules/render_service/core/feature_cfg/xml_parser/performance_feature_parser",
    "$graphic_2d_root/rosen/modules/render_service_client/core",
    "$graphic_2d_root/rosen/modules/render_service_base/src",
    "$graphic_2d_root/rosen/test/include",
    "$graphic_2d_root/rosen/test/render_service/render_service/unittest",
    "//foundation/graphic/graphic_2d/rosen/modules/render_service/composer/composer_base/layer_backend",
    "//foundation/graphic/graphic_2d/rosen/modules/render_service/composer/composer_base/render_layer",
    "//foundation/graphic/graphic_2d/rosen/modules/render_service/composer/composer_base/pipeline",
    "//foundation/graphic/graphic_2d/rosen/modules/render_service/composer/composer_base/transaction",
    "//foundation/graphic/graphic_2d/rosen/modules/render_service/composer/composer_client/pipeline",
    "//foundation/graphic/graphic_2d/rosen/modules/render_service/composer/composer_base/connection",
    "//foundation/graphic/graphic_2d/rosen/modules/render_service/composer/composer_client/layer",
  ]

  cflags = [
    "-Wall",
    "-Werror",
    "-g3",
    "-Dprivate=public",
    "-Dprotected=public",
  ]
}

ohos_static_library("rs_test_common") {
  visibility = [ ":*" ]
  testonly = true
  defines = []
  defines += gpu_defines
  sources = [ "../../pipeline/rs_test_util.cpp" ]
  public_configs = [ ":rs_test_common_public_config" ]

  external_deps = []
  if (defined(global_parts_info) && defined(global_parts_info.sensors_sensor)) {
    external_deps += [ "sensor:sensor_interface_native" ]
  }

  subsystem_name = "graphic"
  part_name = "graphic_2d"
}
