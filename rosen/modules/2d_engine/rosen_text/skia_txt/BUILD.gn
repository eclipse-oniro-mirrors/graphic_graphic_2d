# Copyright (c) 2024 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/graphic/graphic_2d/ace_platforms.gni")
import("//foundation/graphic/graphic_2d/graphic_config.gni")
import("$graphic_2d_root/rosen/modules/2d_engine/rosen_text/config.gni")

import("//build/ohos_var.gni")

config("skia_libtxt_config") {
  include_dirs = [
    "$graphic_2d_root/rosen/modules/2d_engine/rosen_text/export",
    "$graphic_2d_root/rosen/modules/2d_engine/rosen_text/export/rosen_text",
    "$graphic_2d_root/rosen/modules/2d_engine/rosen_text/export/skia_txt",
  ]

  cflags_cc = [
    "-Wno-implicit-fallthrough",
    "-fvisibility-inlines-hidden",
    "-std=c++17",
  ]
}

if (use_new_skia) {
  skia_root = "//third_party/skia/m133"
} else {
  skia_root = "//third_party/skia"
}

txt_root = "$rosen_root/modules/2d_engine/rosen_text/skia_txt/"
skunicode_root = "$skia_root/modules/skunicode"
skshaper_root = "$skia_root/modules/skshaper"

template("skia_libtxt") {
  forward_variables_from(invoker, "*")

  ohos_source_set(target_name) {
    part_name = "graphic_2d"
    subsystem_name = "graphic"
    defines += invoker.defines
    cflags_cc += invoker.cflags_cc

    public_configs = [ ":skia_libtxt_config" ]
    include_dirs = [
      "$rosen_root/modules/render_service_base/include",
      "$graphic_2d_root/rosen/modules/2d_graphics/include",
      "$graphic_2d_root/rosen/modules/2d_graphics/src",
      "$graphic_2d_root/rosen/modules/2d_graphics/src/drawing",
      "$graphic_2d_root/rosen/modules/2d_graphics/src/drawing/engine_adapter",
      "$graphic_2d_root/rosen/modules/2d_engine/rosen_text/adapter/skia_txt",
      "$graphic_2d_root/rosen/modules/2d_engine/rosen_text/skia_txt",
      "$graphic_2d_root/rosen/modules/2d_engine/rosen_text/skia_txt/txt",
      "$graphic_2d_root/rosen/modules/texgine/src",
    ]

    sources = [
      "$txt_root/common_utils/string_util.cpp",
      "$txt_root/impl/drawing_painter_impl.cpp",
      "$txt_root/impl/paragraph_builder_impl.cpp",
      "$txt_root/impl/paragraph_impl.cpp",
      "$txt_root/impl/paragraph_line_fetcher_impl.cpp",
      "$txt_root/impl/paragraph_relayout_impl.cpp",
      "$txt_root/impl/run_impl.cpp",
      "$txt_root/impl/text_font_utils.cpp",
      "$txt_root/impl/text_line_impl.cpp",
      "$txt_root/symbol_engine/hm_symbol_node_build.cpp",
      "$txt_root/symbol_engine/hm_symbol_run.cpp",
      "$txt_root/symbol_engine/hm_symbol_txt.cpp",
      "$txt_root/symbol_resource/symbol_config_parser.cpp",
      "$txt_root/txt/asset_font_manager.cpp",
      "$txt_root/txt/font_asset_provider.cpp",
      "$txt_root/txt/font_collection.cpp",
      "$txt_root/txt/paragraph_builder.cpp",
      "$txt_root/txt/paragraph_style.cpp",
      "$txt_root/txt/placeholder_run.cpp",
      "$txt_root/txt/platform.cpp",
      "$txt_root/txt/text_bundle_config_parser.cpp",
      "$txt_root/txt/text_style.cpp",
      "$txt_root/txt/typeface_font_asset_provider.cpp",
    ]

    external_deps = [ "jsoncpp:jsoncpp" ]
    if (platform == "ohos" || platform == "ohos_ng") {
      external_deps += [
        "bundle_framework:appexecfwk_base",
        "bundle_framework:appexecfwk_core",
        "init:libbegetutil",
        "ipc:ipc_core",
        "samgr:samgr_proxy",
      ]
      defines += [ "HM_SYMBOL_TXT_ENABLE" ]
      defines += [ "OHOS_TEXT_ENABLE" ]
    } else {
      defines += [ "MODULE_TEXTING" ]
    }

    if (use_new_skia) {
      defines += [ "ENABLE_TEXT_ENHANCE" ]
    } else {
      defines += [ "OHOS_SUPPORT" ]
    }

    is_cross_platform =
        current_os == "mac" || current_os == "mingw" || current_os == "linux" ||
        current_os == "android" || current_os == "ios"

    if (defined(use_rosen_drawing) && use_rosen_drawing) {
      if (use_new_skia) {
        defines += [
          "USE_ROSEN_DRAWING",
          "ENABLE_DRAWING_ADAPTER",
          "USE_M133_SKIA",
        ]
      } else {
        defines += [
          "USE_ROSEN_DRAWING",
          "USE_SKIA_TXT",
        ]
      }
      if (rs_enable_gpu) {
        defines += [ "RS_ENABLE_GPU" ]
      }
    }

    deps = [
      ":skia_paragraph",
      ":skia_shaper",
      ":skia_unicode",
    ]

    if (is_arkui_x) {
      deps += [
        "//third_party/bounds_checking_function:libsec_static",
        "//third_party/jsoncpp:jsoncpp_static",
        "$skia_root:skia_$platform",
      ]
    } else {
      external_deps += [
        "bounds_checking_function:libsec_shared",
        "hilog:libhilog",
        "skia:skia_canvaskit",
      ]
      defines += [ "OHOS_STANDARD_SYSTEM" ]
    }

    if (!is_cross_platform) {
      external_deps += [ "hitrace:hitrace_meter" ]
    }
  }
}

foreach(item, ace_platforms) {
  skia_libtxt("skia_libtxt_" + item.name) {
    platform = item.name
    defines = []
    cflags_cc = []
    config = {
    }

    if (defined(item.config)) {
      config = item.config
    }

    if (defined(config.defines)) {
      defines = config.defines
    }

    if (defined(config.cflags_cc)) {
      cflags_cc = config.cflags_cc
    }
  }
}

ohos_source_set("skia_paragraph") {
  part_name = "graphic_2d"
  subsystem_name = "graphic"

  public_configs = [ ":skia_libtxt_config" ]
  include_dirs = [
    "$rosen_root/modules/render_service_base/include",
    "$graphic_2d_root/rosen/modules/2d_graphics/include",
    "$graphic_2d_root/rosen/modules/2d_graphics/src",
    "$graphic_2d_root/rosen/modules/2d_graphics/src/drawing",
    "$graphic_2d_root/rosen/modules/2d_graphics/src/drawing/engine_adapter",
    "$skia_root/modules/skparagraph",
  ]

  if (use_new_skia) {
    defines = [
      "ENABLE_TEXT_ENHANCE",
      "SK_UNICODE_ICU_IMPLEMENTATION"
    ]
  } else {
    defines = [ "OHOS_SUPPORT" ]
  }

  sources = [
    "$skia_root/modules/skparagraph/src/Decorations.cpp",
    "$skia_root/modules/skparagraph/src/FontArguments.cpp",
    "$skia_root/modules/skparagraph/src/FontCollection.cpp",
    "$skia_root/modules/skparagraph/src/HyphenTrie.cpp",
    "$skia_root/modules/skparagraph/src/Hyphenator.cpp",
    "$skia_root/modules/skparagraph/src/OneLineShaper.cpp",
    "$skia_root/modules/skparagraph/src/ParagraphBuilderImpl.cpp",
    "$skia_root/modules/skparagraph/src/ParagraphCache.cpp",
    "$skia_root/modules/skparagraph/src/ParagraphImpl.cpp",
    "$skia_root/modules/skparagraph/src/ParagraphLineFetcherImpl.cpp",
    "$skia_root/modules/skparagraph/src/ParagraphPainterImpl.cpp",
    "$skia_root/modules/skparagraph/src/ParagraphStyle.cpp",
    "$skia_root/modules/skparagraph/src/Run.cpp",
    "$skia_root/modules/skparagraph/src/RunBaseImpl.cpp",
    "$skia_root/modules/skparagraph/src/TextLine.cpp",
    "$skia_root/modules/skparagraph/src/TextLineBaseImpl.cpp",
    "$skia_root/modules/skparagraph/src/TextLineJustify.cpp",
    "$skia_root/modules/skparagraph/src/TextShadow.cpp",
    "$skia_root/modules/skparagraph/src/TextStyle.cpp",
    "$skia_root/modules/skparagraph/src/TextTabAlign.cpp",
    "$skia_root/modules/skparagraph/src/TextWrapper.cpp",
    "$skia_root/modules/skparagraph/src/TypefaceFontProvider.cpp",
  ]

  if (use_new_skia) {
    sources += [
      "$skia_root/modules/skparagraph/src/TextGlobalConfig.cpp",
    ]
  }

  if (defined(use_rosen_drawing) && use_rosen_drawing) {
    if (use_new_skia) {
      defines += [
        "USE_ROSEN_DRAWING",
        "ENABLE_DRAWING_ADAPTER",
        "USE_M133_SKIA",
      ]
    } else {
      defines += [
        "USE_ROSEN_DRAWING",
        "USE_SKIA_TXT",
      ]
    }
    if (rs_enable_gpu) {
      defines += [ "RS_ENABLE_GPU" ]
    }
  }

  platform = current_os
  if (platform == "mingw") {
    platform = "windows"
  }
  deps = [
    ":skia_shaper",
    ":skia_unicode",
  ]

  if (is_arkui_x) {
    deps += [
      "//base/hiviewdfx/hilog/interfaces/native/innerkits:libhilog_${target_os}",
      "//third_party/bounds_checking_function:libsec_static",
      "//third_party/icu/icu4c:static_icuuc",
      "$skia_root:skia_$platform",
    ]
  } else {
    external_deps = [
      "bounds_checking_function:libsec_shared",
      "hilog:libhilog",
      "icu:shared_icuuc",
      "skia:skia_canvaskit",
    ]
  }

  is_cross_platform =
      current_os == "mac" || current_os == "mingw" || current_os == "linux" ||
      current_os == "android" || current_os == "ios"

  if (!is_cross_platform) {
    defines += [ "OHOS_TEXT_ENABLE" ]
    external_deps += [
      "hitrace:hitrace_meter",
      "init:libbegetutil",
    ]
  }
}

ohos_source_set("skia_unicode") {
  part_name = "graphic_2d"
  subsystem_name = "graphic"

  public_configs = [ ":skia_libtxt_config" ]
  include_dirs = []

  if (use_new_skia) {
    defines = [ "ENABLE_TEXT_ENHANCE" ]
  } else {
    defines = [ "OHOS_SUPPORT" ]
  }

  sources = []

  public = [ "$skunicode_root/include/SkUnicode.h" ]
  defines += [ "SKUNICODE_IMPLEMENTATION=1" ]
  defines += [ "SK_UNICODE_AVAILABLE" ]
  defines += [ "SK_UNICODE_ICU_IMPLEMENTATION" ]
  if (!is_arkui_x) {
    sources += [ "$skunicode_root/src/SkUnicode.cpp" ]
    sources += [
      "$skunicode_root/src/SkUnicode_icu.cpp",
      "$skunicode_root/src/SkUnicode_icu.h",
      "$skunicode_root/src/SkUnicode_icu_bidi.cpp",
      "$skunicode_root/src/SkUnicode_icu_bidi.h",
    ]
    sources += [ "$skunicode_root/src/SkUnicode_icu_builtin.cpp" ]
  }

  platform = current_os
  if (platform == "mingw") {
    platform = "windows"
  }
  external_deps = [
    "icu:shared_icuuc",
    "skia:skia_canvaskit",
  ]
}

ohos_source_set("skia_shaper") {
  part_name = "graphic_2d"
  subsystem_name = "graphic"

  public_configs = [ ":skia_libtxt_config" ]
  include_dirs = [
    "$rosen_root/modules/render_service_base/include",
    "$graphic_2d_root/rosen/modules/2d_graphics/include",
    "$graphic_2d_root/rosen/modules/2d_graphics/src",
    "$graphic_2d_root/rosen/modules/2d_graphics/src/drawing",
    "$graphic_2d_root/rosen/modules/2d_graphics/src/drawing/engine_adapter",
  ]
  defines = []

  if (defined(use_rosen_drawing) && use_rosen_drawing) {
    if (use_new_skia) {
      defines += [
        "USE_ROSEN_DRAWING",
        "ENABLE_DRAWING_ADAPTER",
        "USE_M133_SKIA",
      ]
    } else {
      defines += [
        "USE_ROSEN_DRAWING",
        "USE_SKIA_TXT",
      ]
    }
    if (rs_enable_gpu) {
      defines += [ "RS_ENABLE_GPU" ]
    }
  }

  defines += [ "SKSHAPER_IMPLEMENTATION=1" ]
  sources = [
    "$skshaper_root/src/SkShaper.cpp",
    "$skshaper_root/src/SkShaper_harfbuzz.cpp",
    "$skshaper_root/src/SkShaper_primitive.cpp",
  ]

  platform = current_os
  if (platform == "mingw") {
    platform = "windows"
  }
  deps = [ ":skia_unicode" ]

  if (is_arkui_x) {
    deps += [
      "//third_party/bounds_checking_function:libsec_static",
      "$skia_root:skia_$platform",
    ]
  } else {
    external_deps = [
      "bounds_checking_function:libsec_shared",
      "skia:skia_canvaskit",
    ]
  }
}
