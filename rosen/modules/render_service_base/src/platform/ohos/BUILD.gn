# Copyright (c) 2021-2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/graphic/graphic_2d/graphic_config.gni")
import("../../../config.gni")

group("ohos") {
  public_deps = [ ":rosen_ohos_sources" ]
}

config("export_config") {
  include_dirs = [
    "$graphic_2d_root/rosen/include",
    "$graphic_2d_root/rosen/modules/composer/vsync/include",
    "//commonlibrary/c_utils/base/include",
    "//utils/system/safwk/native/include",
    "$graphic_2d_root/frameworks/surface/src",
    "//foundation/multimedia/image_framework/interfaces/innerkits/include",
  ]
}

ohos_source_set("rosen_ohos_sources") {
  public_configs = [ ":export_config" ]

  defines = [ "ROSEN_OHOS" ]
  defines += gpu_defines

  sources = [
    #backend
    "backend/rs_surface_frame_ohos_raster.cpp",
    "backend/rs_surface_ohos_raster.cpp",
    "rs_ashmem_helper.cpp",

    #overdraw
    "overdraw/rs_cpu_overdraw_canvas_listener.cpp",
    "overdraw/rs_gpu_overdraw_canvas_listener.cpp",
    "overdraw/rs_listened_canvas.cpp",
    "overdraw/rs_overdraw_controller.cpp",
    "rs_event_detector.cpp",
    "rs_event_manager.cpp",
    "rs_frame_report.cpp",
    "rs_innovation.cpp",
    "rs_log.cpp",
    "rs_marshalling_helper.cpp",
    "rs_render_service_client.cpp",
    "rs_render_service_connect_hub.cpp",
    "rs_render_service_connection_proxy.cpp",
    "rs_render_service_proxy.cpp",
    "rs_surface_frame_ohos.cpp",
    "rs_surface_ohos.cpp",
    "rs_surface_ohos_converter.cpp",
    "rs_system_properties.cpp",
    "rs_vsync_client_ohos.cpp",
  ]

  if (accessibility_enable) {
    sources += [ "rs_accessibility.cpp" ]
  }

  if (ace_enable_gpu) {
    sources += [
      "backend/rs_surface_frame_ohos_gl.cpp",
      "backend/rs_surface_ohos_gl.cpp",
    ]

    if (graphic_standard_feature_enable_vulkan) {
      sources += [
        "backend/rs_surface_frame_ohos_vulkan.cpp",
        "backend/rs_surface_ohos_vulkan.cpp",
      ]
    }

    sources += [
      "$graphic_2d_root/rosen/modules/2d_graphics/src/render_context/cache_data.cpp",
      "$graphic_2d_root/rosen/modules/2d_graphics/src/render_context/memory_handler.cpp",
      "$graphic_2d_root/rosen/modules/2d_graphics/src/render_context/shader_cache.cpp",
    ]

    sources += [
      "$graphic_2d_root/rosen/modules/2d_graphics/src/render_context/render_context.cpp",
      "$graphic_2d_root/rosen/modules/2d_graphics/src/render_context/render_context_egl.cpp",
    ]
  }

  include_dirs = [
    "$graphic_2d_root/rosen/modules/render_service_base/include",
    "$graphic_2d_root/rosen/modules/render_service_base/src",
    "//commonlibrary/c_utils/base/include",
    "//utils/system/safwk/native/include",
    "$graphic_2d_root/prebuilts/librarys/gpu/include",
    "//drivers/peripheral/display/interfaces/include/",
    "$graphic_2d_root/rosen/modules/render_service_client/core",
    "$graphic_2d_root/utils/log",
  ]

  public_deps = [
    "$graphic_2d_root:libsurface",
    "$graphic_2d_root/rosen/modules/2d_graphics:2d_graphics",
    "$graphic_2d_root/rosen/modules/composer/vsync:libvsync",
    "$graphic_2d_root/utils:sync_fence",
    "//commonlibrary/c_utils/base:utils",
    "//foundation/multimedia/image_framework/interfaces/innerkits:image_native",
  ]
  if (defined(use_new_skia) && use_new_skia) {
    public_deps += [
      "//third_party/skia:skia_ohos",
    ]
  } else {
    public_deps += [ "//third_party/flutter/build/skia:ace_skia_ohos" ]
  }

  if (rs_enable_gpu) {
    include_dirs += [
      "//third_party/openssl/include",
      "$graphic_2d_root/frameworks/surface/include",
      "$graphic_2d_root/interfaces/inner_api/common",
    ]
    if (defined(use_new_skia) && use_new_skia) {
      include_dirs += [ "//third_party/skia" ]
    } else {
      include_dirs += [ "//third_party/flutter/skia" ]
    }

    if (graphic_standard_feature_enable_vulkan) {
      include_dirs += [ "//third_party/flutter/engine/flutter/vulkan" ]
    }
    public_deps += [
      "$graphic_2d_root:libgl",
      "$graphic_2d_root:libvulkan",
      "//third_party/openssl:libcrypto_shared",
    ]
  }

  external_deps = [
    "eventhandler:libeventhandler",
    "hisysevent_native:libhisysevent",
    "hitrace_native:hitrace_meter",
    "hiviewdfx_hilog_native:libhilog",
    "init:libbeget_proxy",
    "init:libbegetutil",
    "ipc:ipc_core",
    "samgr:samgr_proxy",
  ]

  if (accessibility_enable) {
    external_deps += [ "accessibility:accessibilityconfig" ]
    defines += accessibility_defines
  }

  if (defined(graphic_2d_ext_configs.vendor_root)) {
    sources -= [
      "backend/rs_surface_ohos_gl.cpp",
      "rs_render_service_client.cpp",
      "rs_render_service_connect_hub.cpp",
    ]

    include_dirs += graphic_2d_ext_configs.librender_service_base_platform_ohos_ext_include_dirs

    sources +=
        graphic_2d_ext_configs.librender_service_base_platform_ohos_ext_sources

    if (ace_enable_gpu && is_emulator) {
      sources -= [ "$graphic_2d_root/rosen/modules/2d_graphics/src/render_context/render_context.cpp" ]
      sources -= [ "$graphic_2d_root/rosen/modules/2d_graphics/src/render_context/render_context.mm" ]
      sources +=
          graphic_2d_ext_configs.librender_service_base_2d_graphics_ext_sources
    }

    if ((current_os == "ios" && !ace_enable_gpu) || current_os == "android") {
      sources -= [ "$graphic_2d_root/rosen/modules/2d_graphics/src/render_context/render_context_egl.cpp" ]
    }
  }

  if (defined(global_parts_info) &&
      defined(global_parts_info.resourceschedule_soc_perf)) {
    external_deps += [ "resource_schedule_service:ressched_client" ]
    defines += [ "RES_SCHED_ENABLE" ]
  }
}
