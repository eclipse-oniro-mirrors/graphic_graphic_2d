# Copyright (c) 2024 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("graphic_config.gni")

use_mingw_win = "${current_os}_${current_cpu}" == "mingw_x86_64"
graphic_2d_root = "//foundation/graphic/graphic_2d"
ace_platforms = []

foreach(item, adapters) {
  import_var = {
  }
  import_var = {
    import("$graphic_2d_root/adapter/$item/build/platform.gni")
  }

  if (defined(import_var.platforms)) {
    foreach(platform, import_var.platforms) {
      if (!is_arkui_x ||
          (is_arkui_x && defined(platform.cross_platform_support) &&
           platform.cross_platform_support)) {
        if (defined(platform.name)) {
          ace_platforms += [ platform ]
        }
      }
    }
  }
}
