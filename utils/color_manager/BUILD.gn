# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/graphic/graphic_2d/graphic_config.gni")

platform = current_os
if (current_os == "mingw") {
  platform = "windows"
}

## Build color_manager.so {{{
config("color_manager_config") {
  visibility = [ ":*" ]

  cflags = [
    "-Wall",
    "-Werror",
    "-g3",
    "-Wno-error=missing-braces",
    "-Wno-error=#warnings",
    "-Os",
  ]

  ldflags = [ "-Wl,--no-as-needed" ]
}

config("color_manager_public_config") {
  include_dirs = [ "export" ]
  if (is_arkui_x) {
    include_dirs += [ "$skia_root_new" ]
  }
}

template("color_manager_source_set") {
  forward_variables_from(invoker, "*")

  ohos_source_set(target_name) {
    sources = [
      "src/color.cpp",
      "src/color_space.cpp",
      "src/color_space_convertor.cpp",
    ]

    configs = [ ":color_manager_config" ]
    public_configs = [ ":color_manager_public_config" ]

    external_deps = [ "skia:skia_canvaskit" ]

    part_name = "graphic_2d"
    subsystem_name = "graphic"
  }
}

color_manager_source_set("color_manager_source") {
}

config("export") {
  include_dirs = [ "export" ]
}

if (current_os == "ohos") {
  ohos_shared_library("color_manager") {
    branch_protector_ret = "pac_ret"
    deps = [ ":color_manager_source" ]
    public_external_deps = [ "bounds_checking_function:libsec_shared" ]
    public_configs = [ ":export" ]
    public_external_deps += [ "skia:skia_canvaskit" ]
    subsystem_name = "graphic"
    part_name = "graphic_2d"
  }
} else {
  if (is_arkui_x) {
    ohos_source_set("color_manager") {
      public_deps = [ ":color_manager_source" ]
      part_name = "graphic_2d"
      subsystem_name = "graphic"
    }
  } else {
    ohos_shared_library("color_manager") {
      deps = [ ":color_manager_source" ]
      public_configs = [ ":export" ]
      part_name = "graphic_2d"
      subsystem_name = "graphic"
    }
  }
}

group("test") {
  testonly = true
  deps = [ "test:test" ]
}

## Build color_manager.so }}}

