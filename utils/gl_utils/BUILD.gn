# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/graphic/graphic_2d/graphic_config.gni")

## Build gl_utils.a {{{
config("gl_utils_config") {
  visibility = [ ":*" ]

  cflags = [
    "-Wall",
    "-Werror",
    "-g3",
  ]

  if (!ace_enable_gpu) {
    cflags += [ "-Wno-unused-private-field" ]
  }
}

config("gl_utils_public_config") {
  include_dirs = [ "export" ]
}

ohos_static_library("gl_utils") {
  sources = []

  if (ace_enable_gpu) {
    sources += [
      "src/shader.cpp",
      "src/texture.cpp",
    ]
  } else {
    sources += [
      "src/mock/shader.cpp",
      "src/mock/texture.cpp",
    ]
  }

  configs = [ ":gl_utils_config" ]

  public_configs = [ ":gl_utils_public_config" ]

  public_deps = [
    "$graphic_2d_root/frameworks/opengl_wrapper:EGL",
    "$graphic_2d_root/frameworks/opengl_wrapper:GLESv3",
    "..:libgslogger",
    "..:matrix",
  ]

  part_name = "graphic_2d"
  subsystem_name = "graphic"
}
## Build gl_utils.a }}}
